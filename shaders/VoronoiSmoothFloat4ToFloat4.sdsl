// The MIT License
// Copyright Â© 2014 Inigo Quilez
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions: The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software. THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

// Smooth Voronoi - avoiding aliasing, by replacing the usual min() function, which is
// discontinuous, with a smooth version. That can help preventing some aliasing, and also
// provides with more artistic control of the final procedural textures/models.

// More Voronoi shaders:
//
// Exact edges:  https://www.shadertoy.com/view/ldl3W8
// Hierarchical: https://www.shadertoy.com/view/Xll3zX
// Smooth:       https://www.shadertoy.com/view/ldB3zc
// Voronoise:    https://www.shadertoy.com/view/Xd23Dh

shader VoronoiSmoothFloat4ToFloat4 : Float4ToFloat4, Hash
{

  



	compose ComputeFloat Smoothness; 

    // returns closest, second closest, and cell id
    override float4 Compute(float4 p){
		
		float w = Smoothness.Compute();

        float3 x = p.xyz;
        float3 n = floor( x );
    	float3 f = frac( x );

		float4 m = float4( 8.0, 0.0, 0.0, 0.0 );
        for( int k=-1; k<=1; k++ )
    	for( int j=-2; j<=2; j++ )
    	for( int i=-2; i<=2; i++ )
    	{
        	float3 b = float3(i,j ,k);
        	float3 o = hash33( n + b );
            float3 r = b - f + o;
            float  d = dot( r, r );
		
        	// do the smoth min for colors and distances		
			float cell = hash11(dot(n+b,float3(7.0,113.0, 271.9)));
			float h = smoothstep( 0.0, 1.0, 0.5 + 0.5*(m.x-d)/w );
		
	    	m.xy = lerp( m.xy, float2(d, cell), h ) - h*(1.0-h)*w/(1.0+3.0*w); // distance
			
    	}
	
		return m.yxzw;
    }
};